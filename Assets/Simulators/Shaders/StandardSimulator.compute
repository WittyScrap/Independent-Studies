#pragma kernel CSSimulate
#pragma kernel CSDissipate

#include "PSOComms.cginc"

/** Structures */

//
//  Represents a single particle for the Particle Swarm Optimisation
//  algorithm.
//
struct Particle
{
    float2 position;
    float2 velocity;
    float2 localBest;
};

/** Data */

RWTexture2D<float3>             ParticleSpace;
RWStructuredBuffer<Particle>    Particles;
RWStructuredBuffer<float3>      Reports;

float4 GlobalBest;

int OutputWidth;
int OutputHeight;

float W;
float C1;
float C2;


/** Functions */ 

#include "SolutionSpace.cginc"

float random(in float2 uv)
{
    return frac(sin(dot(uv, float2(12.9898, 78.233))) * 43758.5453123);
}

#define R (random(position) * 2.f)

void iterate(inout float2 velocity, in float2 localBest, in float2 position, in float2 globalBest)
{
    velocity = velocity * W + (localBest - position) * C1 * R + (globalBest - position) * C2 * R;
}

#undef R

void plot(in float2 where)
{
    const float partcileAdd = 0.05f;
    const int radius = 5;

    ParticleSpace[where] += partcileAdd * 2;

    for (int x = where.x - radius; x < where.x + radius; x += 1)
    {
        for (int y = where.y - radius; y < where.y + radius; y += 1)
        {
            ParticleSpace[int2(x, y)] += partcileAdd * (1 - (length(float2(x, y) - where) / (float)radius));
        }
    }
}

float2 iIf(bool _condition, float2 _true, float2 _false)
{
    return (_condition * _true) + (!_condition * _false);
}

/** Kernels */

// PSO kernel
// 
// Runs the Particle Swarm Optimisation algorithm.
[numthreads(1024,1,1)]
void CSSimulate (uint3 id : SV_DispatchThreadID)
{
    // Gather particle data
    int x = id.x;

    float2 position = Particles[x].position;
    float2 velocity = Particles[x].velocity;

    float2 screen = float2(OutputWidth, OutputHeight);


    // Draw particle pixel
    plot(position);


    // PSO simulation
    iterate(velocity, Particles[x].localBest, position, GlobalBest.xy);

    float prevValue = fnSolutionSpace(Particles[x].localBest, screen);
    float bestValue = fnSolutionSpace(GlobalBest.xy, screen);
    float currValue = fnSolutionSpace(position, screen);

    Particles[x].localBest = iIf(currValue CMP prevValue, position, Particles[x].localBest);
    currValue = fnSolutionSpace(Particles[x].localBest, screen);

    float2 globalBest = iIf(currValue CMP bestValue, Particles[x].localBest, GlobalBest.xy);
    bestValue = fnSolutionSpace(globalBest, screen);
    Reports[x] = float3(globalBest, bestValue);

    
    // Advance by current velocity
    position += velocity;


    // Apply particle data
    Particles[x].position = position;
    Particles[x].velocity = velocity;
}

// Dissipation kernel
// 
// Dissipates the trails created by the particles during the main simulation.
[numthreads(32,32,1)]
void CSDissipate(uint3 id : SV_DispatchThreadID)
{
    ParticleSpace[id.xy] = 0;
}